plugins {
	id 'java-library'
	id 'maven-publish'
	id 'net.kyori.indra' version '2.0.6'
	id 'net.kyori.indra.publishing' version '2.0.6'
	id 'net.kyori.blossom' version '1.3.0'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'uk.co.n3fs.mc'
version '5.0.0-SNAPSHOT'
description 'An open-source essential suite for the Velocity proxy'

repositories {
	mavenCentral()

	maven {
        url 'https://repo.velocitypowered.com/snapshots/'
    }

    maven {
        url = 'https://repo.spongepowered.org/maven/'
    }

	maven {
		url = 'https://repo.essentialsx.net/n3fs/'

		credentials {
			username = n3fsUsername
			password = n3fsPassword
		}

		authentication {
			basic(BasicAuthentication)
		}
	}
}

dependencies {
	compileOnly 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'
	annotationProcessor 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'
}

indra {
	github("N3FS", "Neutron") {
		ci(true)
	}
	mitLicense()

	publishReleasesTo("n3fs", "https://repo.essentialsx.net/n3fs/")
	publishSnapshotsTo("n3fs", "https://repo.essentialsx.net/n3fs/")

	configurePublications {
		pom {
			description.set("An open-source essential suite for the Velocity proxy")
			url.set("https://n3fs.co.uk")
			developers {
				developer {
					id.set("mdcfe")
					name.set("MD")
					email.set("md@n3fs.co.uk")
				}
				developer {
					id.set("Crypnotic")
				}
			}
		}
	}

	javaVersions {
		target(11)
		minimumToolchain(16)
	}
}

blossom {
	replaceToken '@ID@', name.toLowerCase()
	replaceToken '@NAME@', name
	replaceToken '@VERSION@', version
	replaceToken '@DESCRIPTION@', description
}

tasks.build {
	dependsOn(tasks.shadowJar)
}
